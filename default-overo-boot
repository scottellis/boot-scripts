setenv loadaddr 0x82000000
setenv console ttyS2,115200n8
setenv mpurate 500
setenv vram 12M
setenv dvimode 1024x768MR-16@60
setenv defaultdisplay dvi
setenv mmcroot "/dev/mmcblk0p2 rw"
setenv mmcrootfstype "ext3 rootwait"
setenv nandroot "/dev/mtdblock4 rw"
setenv nandrootfstype jffs2
setenv mmcargs "setenv bootargs console=\${console} mpurate=\${mpurate} vram=\${vram} omapfb.mode=dvi:\${dvimode} omapfb.debug=y omapdss.def_disp=\${defaultdisplay} root=\${mmcroot} rootfstype=\${mmcrootfstype}"
setenv nandargs "setenv bootargs console=\${console} mpurate=\${mpurate} vram=\${vram} omapfb.mode=dvi:\${dvimode} omapfb.debug=y omapdss.def_disp=\${defaultdisplay} root=\${nandroot} rootfstype=\${nandrootfstype}"
setenv loadbootscript "fatload mmc 0 \${loadaddr} boot.scr"
setenv bootscript "echo Running bootscript from mmc ...\; source \${loadaddr}"
setenv loaduimage "fatload mmc 0 \${loadaddr} uImage"
setenv mmcboot "echo Booting from mmc ...\; run mmcargs\; bootm \${loadaddr}"
setenv nandboot "echo Booting from nand ...\; run nandargs\; nand read \${loadaddr} 280000 400000; bootm \${loadaddr}"
setenv bootcmd "if mmc init\; then if run loadbootscript\; then run bootscript\; else if run loaduimage\; then run mmcboot\; else run nandboot\; fi\; fi\; else run nandboot\; fi"

